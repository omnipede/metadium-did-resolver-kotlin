plugins {
    id 'java'
    id 'jacoco'
    id "org.jetbrains.kotlin.plugin.allopen" version "1.4.32"
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

allOpen {
    annotation("org.springframework.context.annotation.Configuration")
}

group 'io.omnipede'
version '0.0.6'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation group: 'org.web3j', name: 'core', version: '4.8.4'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'

    // Web3j 에서 사용하는 okhttp 와 spring 에서 사용하는 okhttp 간에 버전 충돌이 발생하여 직접 okhttp 의존성을 선언함.
    // 실제 어플리케이션 코드 내에서 참조하고 있지는 않음
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'

    testImplementation ('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        html.enabled true
        csv.enabled true
        xml.enabled true
    }

    afterEvaluate {
        getClassDirectories().setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            // Exclude smart contract wrapper class
                            '**io/omnipede/metadium/did/resolver/infra/contract/IdentityRegistry**',
                            '**io/omnipede/metadium/did/resolver/infra/contract/PublicKeyResolver**',
                            '**io/omnipede/metadium/did/resolver/infra/contract/ServiceKeyResolver**',

                            // Exclude spring application class
                            '**io/omnipede/metadium/did/resolver/ResolverServerApplication**'
                    ])
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }
        }
    }

    afterEvaluate {
        getClassDirectories().setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            // Exclude smart contract wrapper class
                            '**io/omnipede/metadium/did/resolver/infra/contract/IdentityRegistry**',
                            '**io/omnipede/metadium/did/resolver/infra/contract/PublicKeyResolver**',
                            '**io/omnipede/metadium/did/resolver/infra/contract/ServiceKeyResolver**',

                            // Exclude spring application class
                            '**io/omnipede/metadium/did/resolver/ResolverServerApplication**'
                    ])
                })
        )
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}
